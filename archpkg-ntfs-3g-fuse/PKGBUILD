# Maintainer: Cyano Hao <c@cyano.cn>
# Maintainer (extra/ntfs-3g): Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

pkgname=ntfs-3g-fuse
_pkgname=ntfs-3g_ntfsprogs
pkgver=2017.3.23
pkgrel=2
pkgdesc='NTFS-3G with internal fuse'
url='https://www.tuxera.com/community/open-source-ntfs-3g/'
arch=('x86_64')
license=('GPL2')
depends=('util-linux' 'hwinfo')
source=("https://tuxera.com/opensource/${_pkgname}-${pkgver}.tgz"
        'mftmirr.patch')
sha256sums=('3e5a021d7b761261836dcb305370af299793eedbded731df3d6943802e1262d5'
            '4166ff3021921d7a7ffac8f5de64b5735b1d5e365a024744bebc7260cf1ce726')

conflicts=('ntfsprogs' 'ntfs-3g')
provides=('ntfsprogs' 'ntfs-3g')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' -i {ntfsprogs,src}/Makefile.in
	patch -p1 -i ../mftmirr.patch
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbin=/usr/bin \
		--mandir=/usr/share/man \
		--disable-ldconfig \
		--disable-static \
		--with-fuse=internal \
		--enable-xattr-mappings \
		--enable-posix-acls \
		--enable-extras \
		--enable-crypto \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
	rm "${pkgdir}"/usr/share/man/man8/ntfsfallocate.8 # uninstalled binary
	ln -s /usr/bin/ntfs-3g "${pkgdir}/usr/bin/mount.ntfs"
	chmod u+s "${pkgdir}"/usr/bin/{,low}ntfs-3g
}
